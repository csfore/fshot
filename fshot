#!/usr/bin/env perl

use strict;
use warnings;

use File::Find;
use Getopt::Long;
use Pod::Usage;
use Digest::SHA qw(sha256_hex);

my $path = ".";
my @ignore_exts;
my $scan_hidden = 0;
my $quiet = '';
my $help = '';

GetOptions("path=s" => \$path, 
	"ignore-ext=s@" => \@ignore_exts, 
	"scan-hidden" => \$scan_hidden,
	"quiet" => \$quiet,
	"help" => \$help)
	or pod2usage(2);

pod2usage( -verbose => 1) if $help;

my %first_files;
my %second_files;

sub file_hash {
    my ($file_path) = @_;

    my $size = -s $file_path;

    # TODO: I think this block is never used
    if (-d $file_path) {
	return "dir";
    }

    # Getting file contents
    open(my $fh, "<:raw", "$file_path") or die "Failed to open file: $file_path\n";

    # Getting our hash value
    my $sha = Digest::SHA->new(256);
    $sha->addfile($fh);
    my $hash = $sha->hexdigest;

    # No memory leaks!!!!
    close($fh);
    return $hash
}

sub process_file {
    my ($files) = @_;

    # Stipping `./`
    my $file = $File::Find::name;
    $file =~ s{^\./}{};
    

    my $first_char = substr($file, 0, 1);

    # Idk why the second needs to be negated...
    if ($first_char eq '.' && !$scan_hidden) {
	return;
    }

    # Setting a placeholder for directories
    if (-d $file) {
	$files->{$file} = 'dir';
	return;
    }

    if (!$quiet) {
	print "Found: $file\n";
    }
    
    $files->{$file} = file_hash($file);
}


print "Running snapshot 1...\n";

# Enumerating our files
find({ wanted => sub {
    process_file(\%first_files);
     }, no_chdir => 1}, "$path");

print "\nDone. Press enter to run a second snapshot.\n";
# Getting junk data from stdin
<>;

find({ wanted => sub {
    process_file(\%second_files);
     }, no_chdir => 1}, "$path");

# Checking for deletions
for (keys %first_files) {
    if (!$second_files{$_}) {
	print "File deleted: $_\n";
    }
}

# Checking for additions
for (keys %second_files) {
    if (!$first_files{$_}) {
	print "File added: $_\n";
    }
}

# Checking for differences
for (keys %second_files) {
    # TODO: Ignoring deletions, could probably move this up
    if (!$first_files{$_}) {
	next;
    }

    if ($second_files{$_} ne $first_files{$_}) {
	print "File content modified: $_\n";
    }
}

__END__

=head1 NAME

fshot - Filesystem Snapshotter

=head1 SYNOPSIS

fshot [options]

=head1 OPTIONS

=over 4

=item B<--help>

Prints this menu

=item B<--path>

Path to snapshot

=item B<--ignore-ext>

An extension to ignore (can provide multiple)

=item B<--ignore-hidden>

Ignores hidden files

=item B<--quiet>

Does not output unnecessary info

=back

=head1 DESCRIPTION

B<fshot> will snapshot a filesystem twice and compare the differences

=cut
